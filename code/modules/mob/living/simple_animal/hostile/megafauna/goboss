//DARK WIZARD (basically gob)
//The evil guy getting his kicks watching the two kingdoms fight for eternity
/mob/living/simple_animal/hostile/boss/dark_wizard
	name = "Dark Wizard"
	desc = "The dark wizard who trapped the two kingdoms here, they appear to have gone lichy in their old age."
	boss_abilities = list(/datum/action/boss/wizard_surround, /datum/action/boss/wizard_summon_knights, /datum/action/boss/wizard_slippy)
	faction = list("hostile","boss")
	del_on_death = TRUE
	loot = list(/obj/item/clothing/suit/wizrobe/black,
				/obj/item/clothing/head/wizard/black,
				/obj/effect/decal/remains/human)
	icon_state = "dark_wizard"
	ranged = 1
	minimum_distance = 3
	retreat_distance = 3
	melee_damage_lower = 10
	melee_damage_upper = 20
	health = 1000
	maxHealth = 1000
	projectiletype = /obj/item/projectile/magic/pain
	projectilesound = 'sound/weapons/emitter.ogg'
	attack_sound = 'sound/hallucinations/growl1.ogg'
	var/list/copies = list()

//Hit the real guy? Copies are destroyed.
/mob/living/simple_animal/hostile/boss/dark_wizard/adjustHealth(amount)
	if(amount > 0)//damage
		minimum_distance = 3
		retreat_distance = 3
		for(var/copy in copies)
			qdel(copy)
	..()



//WIZARD SURROUND
//Surrounds the target with 3 fake wizards, and the real one
//hitting the real one kills the fakes
//hitting a fake deals 50 damage to everyone nearby (except the real wizard)
//this damage will hurt any fallen knights he's summoned, that's how little he cares about them

/mob/living/simple_animal/hostile/boss/dark_wizard/copy
	desc = "An illusion, no doubt.."
	health = 1
	maxHealth = 1
	alpha = 200
	boss_abilities = list()
	melee_damage_lower = 1
	melee_damage_upper = 5
	minimum_distance = 0
	retreat_distance = 0
	ranged = 0
	loot = list() //not even bones, they were never real! ~spook~
	var/mob/living/simple_animal/hostile/boss/dark_wizard/original


//Hit a fake? Pain.
/mob/living/simple_animal/hostile/boss/dark_wizard/copy/adjustHealth(amount)
	if(amount > 0) //damage
		if(original)
			original.minimum_distance = 3
			original.retreat_distance = 3
			original.copies -= src
			for(var/c in original.copies)
				qdel(c)
		for(var/mob/living/L in range(5,src))
			if(L == original || istype(L, type))
				continue
			L.adjustBruteLoss(50)
		qdel(src)
	else
		..()

/mob/living/simple_animal/hostile/boss/dark_wizard/copy/examine(mob/user)
	..()
	qdel(src) //I see through your ruse!


/datum/action/boss/wizard_surround
	name = "Surround"
	button_icon_state = "wizard_surround"
	usage_probability = 30
	boss_cost = 20
	boss_type = /mob/living/simple_animal/hostile/boss/dark_wizard
	say_when_triggered = ""

/datum/action/boss/wizard_surround/Trigger()
	if(..())
		var/mob/living/target
		if(!boss.client) //AI's target
			target = boss.target
		else //random mob
			var/list/threats = boss.PossibleThreats()
			if(threats.len)
				target = pick(threats)
		if(target)
			var/mob/living/simple_animal/hostile/boss/dark_wizard/wiz = boss
			var/directions = cardinal.Copy()
			for(var/i in 1 to 3)
				var/mob/living/simple_animal/hostile/boss/dark_wizard/copy/C = new (get_step(target,pick_n_take(directions)))
				wiz.copies += C
				C.original = wiz
				C.say("You were a fool to come here.")
			wiz.say("You were a fool to come here.")
			wiz.forceMove(get_step(target,pick_n_take(directions)))
			wiz.minimum_distance = 1 //so he doesn't run away and ruin everything
			wiz.retreat_distance = 0
		else
			boss.atb.refund(boss_cost)



//SUMMON KNIGHTS
//Summons 4 AI Knights, 2 from each kingdom

/datum/action/boss/wizard_summon_knights
	name = "Summon Knights"
	button_icon_state = "knight_summon"
	usage_probability = 40
	boss_cost = 40
	boss_type = /mob/living/simple_animal/hostile/boss/dark_wizard
	needs_target = FALSE
	say_when_triggered = "I will see to it that you are killed as painfully as I know how."

/datum/action/boss/wizard_summon_knights/Trigger()
	if(..())
		var/list/knights = list(
		/mob/living/simple_animal/hostile/skeleton/templar/dark_wiz/knottingham,
		/mob/living/simple_animal/hostile/skeleton/templar/dark_wiz/plappington,
		/mob/living/simple_animal/hostile/skeleton/templar/dark_wiz/knottingham,
		/mob/living/simple_animal/hostile/skeleton/templar/dark_wiz/plappington)
		var/list/directions = cardinal.Copy()
		for(var/i in 1 to 4)
			var/knight = pick_n_take(knights)
			new knight (get_step(boss,pick_n_take(directions)))


/mob/living/simple_animal/hostile/skeleton/templar/dark_wiz
	faction = list("hostile","dark magicks")
	del_on_death = TRUE
	deathmessage = "The knight collaspes into a pile of bones!"

/mob/living/simple_animal/hostile/skeleton/templar/dark_wiz/knottingham
	name = "fallen knight of Knottingham"
	desc = "They belong to the dark wizard now..."
	speak = list("For Knotti--.. the dark wizard!","AAAAAAAAAAAAAAAH!!")
	icon_state = "knot_knight"
	loot = list(/obj/item/clothing/suit/armor/riot/knight/red,
				/obj/item/weapon/shield/riot/buckler,
				/obj/item/weapon/claymore,
				/obj/effect/decal/remains/human)

/mob/living/simple_animal/hostile/skeleton/templar/dark_wiz/plappington
	name = "fallen knight of Plappington"
	desc = "They belong to the dark wizard now..."
	speak = list("For Plap.. the dark wizard!","AAAAARRRRGGGGGGH!!")
	icon_state = "plap_knight"
	loot = list(/obj/item/clothing/suit/armor/riot/knight/blue,
				/obj/item/weapon/shield/riot/buckler,
				/obj/item/weapon/claymore,
				/obj/effect/decal/remains/human)



//WIZARD SLIPPY
//Spawn lube around the wizard
//This is as evil as it sounds
//I can't really say I'm sorry either

/datum/action/boss/wizard_slippy
	name = "Grease" //lightning
	button_icon_state = "grease"
	usage_probability = 20
	boss_cost = 60
	boss_type = /mob/living/simple_animal/hostile/boss/dark_wizard
	say_when_triggered = "Feel your grip on life slipping away yet?"

/datum/action/boss/wizard_slippy/Trigger()
	if(..())
		for(var/turf/simulated/S in range(5,boss))
			S.MakeSlippery(TURF_WET_LUBE)

